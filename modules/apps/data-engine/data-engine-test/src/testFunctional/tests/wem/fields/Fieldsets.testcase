@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@ignore = "true"
	@priority = "4"
	test AddFieldsetOnBuilderWithSameFieldType {
		property portal.acceptance = "false";

		// TODO LPS-107630 AddFieldsetOnBuilderWithSameFieldType pending implementatio

	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@ignore = "true"
	@priority = "4"
	test AddFieldsetOnOtherFieldsetAlreadyOnBuilder {
		property portal.acceptance = "false";

		// TODO LPS-107630 AddFieldsetOnOtherFieldsetAlreadyOnBuilder pending implementation
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "5"
	test DuplicateFieldset {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "DuplicateTest",
			structureName = "DuplicateTest");

		DataEngine.addFields(fieldNameList = "Text,Image,Upload");

		WebContentStructures.saveCP(structureName = "DuplicateTest");

		WebContentStructures.addCP(
			structureDescription = "DuplicateTest1",
			structureName = "DuplicateTest1");

		DataEngine.addFieldsets(
			fieldsetLabel = "DuplicateTest",
			fieldsetName = "DuplicateTest");

		DataEngine.duplicate(fieldsetLabel = "DuplicateTest");

		AssertElementPresent(
			key_fieldsetLabel = "Copy of DuplicateTest",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		for (var fieldLabelList : list "Copy of Text,Copy of Image,Copy of Upload") {
			AssertElementPresent(
				key_fieldFieldLabel = "${fieldLabelList}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");
		}

		WebContentStructures.saveCP(structureName = "DuplicateTest1");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "4"
	test EditFieldsetPropertiesFromSideBar {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			createNewFieldset = "true",
			fieldNameList = "Text",
			fieldsetLabel = "New Fieldset",
			fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		DataEngine.gotoFieldsetItemEllipsis(fieldsetName = "New Fieldset");

		MenuItem.click(menuItem = "Edit");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Text Edit",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text Edit",
			fieldHelp = "Edited Help Text",
			fieldsetModal = "true");

		Click(locator1 = "Modal#SAVE_BUTTON");

		Click(locator1 = "Modal#PROPAGATE_BUTTON");

		Alert.viewSuccessMessage();

		AssertVisible(
			key_fieldFieldLabel = "Text Edit",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertVisible(
			key_helpText = "Edited Help Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_HELP_TEXT");

		Button.clickSave();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertVisible(
			key_fieldFieldLabel = "Text Edit",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertVisible(
			key_helpText = "Edited Help Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_HELP_TEXT");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "5"
	test FieldsetNameRenamedWhenNameAlreadyExistent {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

        NavItem.gotoStructures();

        WebContentStructures.addCP(structureName = "WC Structure Name");

        DataEngine.addField(
            fieldFieldLabel = "Text",
            fieldName = "Text");

        DataEngine.editFieldReference(
            assertFieldReference = "Text",
            fieldFieldLabel = "Text",
            fieldReference = "Text");

        DataEngine.addField(
            fieldFieldLabel = "Numeric",
            fieldName = "Numeric");

        DataEngine.editFieldReference(
            assertFieldReference = "Text",
            fieldFieldLabel = "Numeric",
            fieldReference = "Text");

        WebContentStructures.saveCP(structureName = "WC Structure Name");

        WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

        AssertClick.assertPartialTextClickAt(
            key_fieldFieldLabel = "Numeric",
            locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER",
            value1 = "Numeric");

        Navigator.gotoNavTab(navTab = "Advanced");
		
        AssertTextNotEquals(
            key_fieldLabel = "Field Reference",
            key_fieldName = "fieldReference",
            locator1 = "Sidebar#TEXT_INPUT",
            value1 = "Text");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "4"
	test FieldsetSetAsCollapsible {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			createNewFieldset = "true",
			fieldNameList = "Numeric",
			fieldsetLabel = "New Fieldset",
			fieldsetName = "New Fieldset");

		DataEngine.editCollapsible(
			collapsible = "true",
			fieldsetLabel = "New Fieldset");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertVisible(
			key_fieldFieldLabel = "Numeric",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		Click(
			key_label = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_COLLAPSE_BUTTON");

		AssertNotVisible(
			key_fieldFieldLabel = "Numeric",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "4"
	test FieldsetSetAsRepeatable {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			createNewFieldset = "true",
			fieldNameList = "Rich Text",
			fieldsetLabel = "New Fieldset",
			fieldsetName = "New Fieldset");

		DataEngine.editFieldsetsRepeatable(fieldsetLabel = "New Fieldset");

		WebContentStructures.saveCP();

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(webContentTitle = "Web Content Title");

		DataEngine.addRepeatableFieldset(fieldsetLabel = "New Fieldset");

		for (var n : list "1,2") {
			DataEngine.assertFieldRepeated(
				fieldName = "Rich Text",
				index = "${n}");
		}

		AssertElementNotPresent(
			key_fieldName = "FieldsGroup",
			key_index = "1",
			locator1 = "FormFields#FIELD_REMOVE");

		AssertVisible(
			key_fieldName = "FieldsGroup",
			key_index = "2",
			locator1 = "FormFields#FIELD_REMOVE");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		for (var n : list "1,2") {
			DataEngine.assertFieldRepeated(
				fieldName = "Rich Text",
				index = "${n}");
		}

		AssertElementNotPresent(
			key_fieldName = "FieldsGroup",
			key_index = "1",
			locator1 = "FormFields#FIELD_REMOVE");

		AssertVisible(
			key_fieldName = "FieldsGroup",
			key_index = "2",
			locator1 = "FormFields#FIELD_REMOVE");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "4"
	test RepeatFieldsetWithoutDuplicatingContent {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			fieldsetLabel = "Basic Web Content",
			fieldsetName = "Basic Web Content");

		DataEngine.editFieldsetsRepeatable(fieldsetLabel = "Basic Web Content");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(
			webContentContent = "Web Content Content",
			webContentTitle = "Web Content Title");

		DataEngine.addRepeatableFieldset(fieldsetLabel = "Basic Web Content");

		SelectFrame.selectFrameNoLoading(
			key_fieldLabel = "content",
			locator1 = "CKEditor#BODY_FIELD_IFRAME_WEB_CONTENT_ARTICLE");

		AssertTextEquals(
			locator1 = "CKEditor#BODY",
			value1 = "Web Content Content");

		SelectFrame(value1 = "relative=top");

		SelectFrame.selectFrameNoLoading(
			index = "2",
			key_fieldLabel = "content",
			locator1 = "CKEditor#ANY_BODY_FIELD_IFRAME_WEB_CONTENT_ARTICLE");

		AssertTextEquals(
			locator1 = "CKEditor#BODY",
			value1 = "");
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@ignore = "true"
	@priority = "4"
	test SaveStructureAddingBackFieldset {
		property portal.acceptance = "false";

		// TODO LPS-107630 SaveStructureAddingBackFieldset pending implementation
	}

	@description = "This is a test for LPS-107630. As an App Adm, I want to use an existing fieldset"
	@priority = "4"
	test ShowAndHideFieldsetLabel {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			createNewFieldset = "true",
			fieldNameList = "Date",
			fieldsetLabel = "New Fieldset",
			fieldsetName = "New Fieldset");

		DataEngine.editShowOrHideLabel(
			fieldsetLabel = "New Fieldset",
			showLabel = "true");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		DataEngine.editShowOrHideLabel(
			fieldsetLabel = "New Fieldset",
			showLabel = "false");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertElementNotPresent(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-123538. As Content Creator, I cannot edit fieldset on the Advanced panel"
	@priority = "4"
	test VerifyFieldsetCannotBeEditedOnStructureBuilder {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.addFieldsets(
			createNewFieldset = "true",
			fieldNameList = "Upload",
			fieldsetLabel = "New Fieldset",
			fieldsetName = "New Fieldset");

		Click(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_tab = "Basic",
			locator1 = "NavTab#ACTIVE_TAB_LINK");

		AssertElementNotPresent(
			key_tab = "Advanced",
			locator1 = "NavTab#ACTIVE_TAB_LINK");
	}

}
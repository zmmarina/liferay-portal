definition {

	@summary = "Assert Commerce license is deployed after portal is started"
	macro deployCommerceLicense {
		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "deploy-commerce-license-xml -Dliferay.home=${liferayHome}");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "wait-for-license-activation");
	}

	@summary = "Assert Commerce basic functionality is working after activating with licensing"
	macro executeSmoke {
		CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Speedwell");

		CommerceNavigator.gotoPortlet(
			category = "Store Management",
			portlet = "Channels");

		CommerceNavigator.gotoEntry(entryName = "Speedwell Portal");

		CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

		Navigator.openWithAppendToBaseURL(urlAppend = "web/speedwell");

		CommerceNavigator.gotoSpeedwellTopbarItem(menuItem = "Catalog");
	}

	@summary = "Assert Commerce UI is available when activated"
	macro viewCommerceAvailable {
		ApplicationsMenuHelper.openApplicationsMenu();

		AssertElementPresent(
			key_panel = "Applications",
			locator1 = "ApplicationsMenu#PANEL");

		AssertElementPresent(
			key_panel = "Commerce",
			locator1 = "ApplicationsMenu#PANEL");
	}

	@summary = "Assert Commerce UI is unusable from the platform when expired"
	macro viewCommerceExpired {
		CommerceNavigator.gotoPortlet(
			category = "Store Management",
			portlet = "Channels");

		AssertElementNotPresent(locator1 = "ItemSelector#ADD_BUTTON");
	}

	@summary = "Assert Commerce UI is missing from the platform when not activated"
	macro viewCommerceMissing {
		ApplicationsMenuHelper.openApplicationsMenu();

		AssertElementPresent(
			key_panel = "Applications",
			locator1 = "ApplicationsMenu#PANEL");

		AssertElementNotPresent(
			key_panel = "Commerce",
			locator1 = "ApplicationsMenu#PANEL");
	}

	@summary = "Assert expiration date warning for Liferay Commerce Subscription"
	macro viewWarningExpirationDate {
		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Catalogs");

		Alert.viewErrorMessage(errorMessage = "Update your activation key for Liferay Commerce Subscription Development");
		Alert.viewErrorMessage(errorMessage = "expire");
	}

}
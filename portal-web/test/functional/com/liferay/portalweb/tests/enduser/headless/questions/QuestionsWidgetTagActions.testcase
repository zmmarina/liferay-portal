@component-name = "portal-questions"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Questions";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Questions Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "2",
			groupName = "Guest",
			layoutName = "Questions Page",
			widgetName = "Questions");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ProductMenu.gotoPortlet(
				category = "Content &amp; Data",
				portlet = "Message Boards");

			if (IsElementPresent(locator1 = "Questions#QUESTIONS_LIST")) {
				Questions.tearDown();
			}

			Page.tearDownCP();
		}
	}

	@description = "Validate if when the user click in the tag of the Tags tab, the user is redirected to the questions that use this tag"
	@priority = "3"
	@refactordone
	test CanViewQuestionViaTag {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		Questions.addQuestionByAPI(
			bodyField = "Body TagCardRedirectTheUserToTheQuestionsThatAreUsingThisTag",
			categoryName = "Questions Test Category",
			tagField = "tag1",
			titleField = "Title TagCardRedirectTheUserToTheQuestionsThatAreUsingThisTag");

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.viewQuestionViaTag(
			questionTitle = "Title TagCardRedirectTheUserToTheQuestionsThatAreUsingThisTag",
			tagName = "tag1");
	}

	@description = "Validate if Question portlet have one tab with all tags used in the questions"
	@priority = "3"
	@refactordone
	test ListOfTagsCanBeViewedInTagsTab {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Question ListOfTagsAppearsInTheTagsTab",
			userEmailAddress = "test@liferay.com");

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.viewTags(noTagResults = "true");

		Questions.configureTagsFilter(filterBy = "Latest Created");

		Questions.viewTags(noTagResults = "true");

		JSONAssettag.addTag(
			groupName = "Guest",
			tagName = "tag2");

		Questions.addQuestionByAPI(
			bodyField = "Body ListOfTagsAppearsInTheTagsTab",
			categoryName = "Question ListOfTagsAppearsInTheTagsTab",
			tagField = "tag1",
			titleField = "Title ListOfTagsAppearsInTheTagsTab");

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.configureTagsFilter(filterBy = "Latest Created");

		for (var tagName : list "tag1,tag2") {
			Questions.viewTags(tagName = "${tagName}");
		}
	}

	@description = "Validate if the user can sort the tags."
	@priority = "4"
	@refactordone
	@uitest
	test QuestionsTagsCanBeSortedInTagsTab {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Questions Test Category",
			userEmailAddress = "test@liferay.com");

		for (var num : list "1,2") {
			Questions.addQuestionByAPI(
				bodyField = "Question Body ${num}",
				categoryName = "Questions Test Category",
				tagField = "tag1",
				titleField = "Question ${num}");
		}

		Questions.addQuestionByAPI(
			bodyField = "Question Body 3",
			categoryName = "Questions Test Category",
			tagField = "tag2",
			titleField = "Question 3");

		Navigator.gotoPage(pageName = "Questions Page");

		Questions.gotoCategory(categoryName = "Questions Test Category");

		Questions.gotoTab(questionTabField = "Tags");

		Questions.viewTagsFilterResults(
			position = "1",
			tagName = "tag1",
			usedTimes = "Used 2 Times");

		Questions.viewTagsFilterResults(
			position = "2",
			tagName = "tag2",
			usedTimes = "Used 1 Times");

		Questions.configureTagsFilter(filterBy = "Latest Created");

		Questions.viewTagsFilterResults(
			position = "1",
			tagName = "tag2");

		Questions.viewTagsFilterResults(
			position = "2",
			tagName = "tag1");

		Questions.configureTagsFilter(filterBy = "Number of Usages");

		Questions.viewTagsFilterResults(
			position = "1",
			tagName = "tag1",
			usedTimes = "Used 2 Times");

		Questions.viewTagsFilterResults(
			position = "2",
			tagName = "tag2",
			usedTimes = "Used 1 Times");
	}

	@description = "Validate if the Tags tab of the question portlet, the tags are ordered by occurrences"
	@priority = "4"
	@refactordone
	test QuestionTagsCanBeOrderedByOccurrenceInTagsTap {
		for (var num : list "1,2") {
			JSONMBMessage.addCategory(
				categoryDescription = "Description",
				categoryName = "Question ValidateTagsOccurrences${num}",
				userEmailAddress = "test@liferay.com");
		}

		for (var num : list "1,2") {
			Questions.addQuestionByAPI(
				bodyField = "Question Body ${num}",
				categoryName = "Question ValidateTagsOccurrences1",
				tagField = "tag1",
				titleField = "Question ${num}");
		}

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.viewTags(
			amountOfUsedTimes = "2",
			tagName = "tag1");

		Questions.addQuestionByAPI(
			bodyField = "Question Body 3",
			categoryName = "Question ValidateTagsOccurrences2",
			tagField = "tag1",
			titleField = "Question 3");

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.viewTags(
			amountOfUsedTimes = "3",
			tagName = "tag1");
	}

	@description = "This test covers LPS-119780. It ensures that the user can search by tags in the tags search field."
	@priority = "4"
	@refactordone
	test QuestionTagsCanBeSearchedInTagsTab {
		JSONMBMessage.addCategory(
			categoryDescription = "Description",
			categoryName = "Question ValidateSearchByQuestionTags",
			userEmailAddress = "test@liferay.com");

		for (var num : list "1,2,3") {
			Questions.addQuestionByAPI(
				bodyField = "Question Body ${num}",
				categoryName = "Question ValidateSearchByQuestionTags",
				tagField = "tag${num}",
				titleField = "Question ${num}");
		}

		Questions.openToTagsTab(
			layoutName = "questions-page",
			siteURLKey = "guest");

		Questions.search(
			searchKey = "tag1",
			searchTag = "true");

		Questions.viewTags(tagName = "tag1");

		Questions.viewNoTags(tagsList = "tag2,tag3");

		Questions.clearSearchResults();

		for (var tagName : list "tag1,tag2,tag3") {
			Questions.viewTags(tagName = "${tagName}");
		}

		Questions.configureTagsFilter(filterBy = "Latest Created");

		Questions.search(
			searchKey = "tag2",
			searchTag = "true");

		Questions.viewTags(tagName = "tag2");

		Questions.viewNoTags(tagsList = "tag1,tag3");

		Questions.clearSearchResults();

		for (var tagName : list "tag1,tag2,tag3") {
			Questions.viewTags(tagName = "${tagName}");
		}
	}

}
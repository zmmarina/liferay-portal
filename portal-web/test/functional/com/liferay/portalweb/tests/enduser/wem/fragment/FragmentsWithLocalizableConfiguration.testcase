@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-118100. 	Validate if the user can configure other languages to the contributed fragments."
	@priority = "4"
	test ViewContributedFragmentLocalization {
		task ("Add a Heading fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Change to instance language to es-ES") {
			var baseURL = PropsUtil.get("portal.url");
			var currentURL = Navigator.getCurrentURL();
			var localizedURL = StringUtil.replace("${currentURL}", "${baseURL}", "${baseURL}/es");

			Navigator.openSpecificURL(url = "${localizedURL}");
		}

		task ("Assert the fragment name is localized") {
			PageEditor.clickConfigurableField(fragmentName = "Título");

			AssertVisible(
				key_fragmentName = "Título",
				locator1 = "Fragment#FRAGMENT_HEADER");
		}

		task ("Assert the field labels are localized") {
			Click(
				key_navItem = "General",
				locator1 = "PageEditor#NAV_ITEM_BUTTON");

			AssertSelectedLabel(
				key_fieldLabel = "Nivel de título",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "H1");

			Click(
				key_navItem = "Estilos",
				locator1 = "PageEditor#NAV_ITEM_BUTTON");

			for (var fieldSetLabel : list "Margen,Margen interno (padding),Tamaño,Texto,Fondo,Bordes,Efectos") {
				AssertVisible(
					key_fieldSetLabel = "${fieldSetLabel}",
					locator1 = "PageEditor#COMMON_STYLES_FIELDSET_LABEL");
			}

			AssertSelectedLabel(
				key_fieldLabel = "Margen superior",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "0");

			AssertSelectedLabel(
				key_fieldLabel = "Relleno superior",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "0");

			AssertSelectedLabel(
				key_fieldLabel = "Desbordamiento",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");

			AssertSelectedLabel(
				key_fieldLabel = "Familia de fuentes",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");

			AssertSelectedLabel(
				key_fieldLabel = "Familia de fuentes",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");

			AssertSelectedLabel(
				key_fieldLabel = "Origen de la imagen",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Directo");

			AssertSelectedLabel(
				key_fieldLabel = "Radio del borde",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");

			AssertSelectedLabel(
				key_fieldLabel = "Sombra",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Valor por defecto");
		}
	}

	@description = "This is a test for LPS-118100. View localizable fragment configuration field value in translated languages."
	@priority = "4"
	test ViewLocalizableConfigurationFieldValueInTranslatedLanguages {
		task ("Add a fragment collection via JSON") {
			JSONFragment.addFragmentCollection(
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Navigate to fragments admin") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Navigate to the fragment collection") {
			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");
		}

		task ("Add a fragment") {
			FragmentsAdmin.addFragment(fragmentName = "Fragment Name");
		}

		task ("Type html codes in HTML section") {
			var htmlSnippet = '''
<div class="fragment-example">
	<button type="button" class="btn btn-primary">${configuration.buttonText}</button>
</div>
			''';

			FragmentsEditor.typeHtmlEditor(content = "${htmlSnippet}");

			Pause(locator1 = "3000");
		}

		task ("Type css codes in CSS section") {
			var cssSnippet = '''
.fragment-example {
}
		''';

			FragmentsEditor.typeCssEditor(content = "${cssSnippet}");

			Pause(locator1 = "3000");
		}

		task ("Type json codes in Configuration section") {
			FragmentsEditor.gotoConfigurationTab();

			Pause(locator1 = "3000");

			var jsonSnippet = '''
{
	"fieldSets": [
		{
			"label": "Button",
			"fields": [
				{
					"dataType": "string",
					"defaultValue": "Go Somewhere",
					"label": "Button Text",
					"localizable": true,
					"name": "buttonText",
					"type": "text"
				}
			]
		}
	]
}
			''';

			FragmentsEditor.typeConfigurationEditor(content = "${jsonSnippet}");

			Pause(locator1 = "3000");

			PortletEntry.publish();
		}

		task ("Add the new fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				fragmentName = "Fragment Name");
		}

		task ("Change to es_ES via language selector") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Type localized content in Button Text field") {
			PageEditorConfiguration.changeConfigText(
				editableText = "Ir a alguna parte",
				fragmentName = "Fragment Name",
				textConfigName = "buttonText");
		}

		task ("Assert localized content is shown in Button Text field") {
			FormFields.viewTextValue(
				fieldName = "buttonText",
				fieldValue = "Ir a alguna parte");
		}

		task ("Assert localized content is shown in fragment") {
			AssertVisible(
				key_content = "Ir a alguna parte",
				key_fragmentClass = "example",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("Change to en_US via language selector") {
			PageEditor.changeLocale(
				currentLocale = "es-ES",
				locale = "en-US");
		}

		task ("Assert Go Somewhere is shown in Button Text field") {
			FormFields.viewTextValue(
				fieldName = "buttonText",
				fieldValue = "Go Somewhere");
		}

		task ("Assert Go Somewhere is shown in fragment") {
			AssertVisible(
				key_content = "Go Somewhere",
				key_fragmentClass = "example",
				locator1 = "Fragment#FRAGMENT");
		}

		task ("Change to es_ES via language selector") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "es-ES");
		}

		task ("Assert Ir a alguna parte is shown in Button Text field") {
			FormFields.viewTextValue(
				fieldName = "buttonText",
				fieldValue = "Ir a alguna parte");
		}

		task ("Assert Ir a alguna parte is shown in fragment") {
			AssertVisible(
				key_content = "Ir a alguna parte",
				key_fragmentClass = "example",
				locator1 = "Fragment#FRAGMENT");
		}
	}

}
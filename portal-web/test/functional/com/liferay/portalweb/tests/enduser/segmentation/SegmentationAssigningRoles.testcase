@component-name = "portal-segmentation"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Segmentation";

	setUp {
		TestCase.setUpPortalInstance();

		JSONGroup.addGroup(groupName = "Test Site Name");

		task ("Disable email verification for strangers") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Instance Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "User Authentication",
				configurationName = "General",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.uncheckStrangersVerifyEmailCheckbox();
		}

		task ("Add user and set password"){
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Users and Organizations");

			User.editPasswordCP(
				requirePasswordReset = "true",
				userEmailAddress = "userea@liferay.com",
				userScreenName = "usersn");
		}

		task ("Add segment for the user created"){
			ProductMenu.gotoSite(site = "Global");

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Segments");

			LexiconEntry.gotoAdd();

			Segmentation.addProperties(propertyOption = "Screen Name");

			Segmentation.fillTitle(titleField = "Segment usersn");

			Segmentation.fillProperty(propertyField = "usersn");

			Button.clickSave();
		}

		task("Add role and permissions"){
			JSONRole.addRegularRole(
				roleKey = "Roles Regrole Name",
				roleTitle = "Roles Regrole Name");

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "ACCESS_IN_CONTROL_PANEL",
				resourceName = "com_liferay_content_dashboard_web_portlet_ContentDashboardAdminPortlet",
				roleTitle = "Roles Regrole Name");

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "VIEW_CONTROL_PANEL",
				resourceName = "90",
				roleTitle = "Roles Regrole Name");
		}

		task("Assign the segment to the role created"){
			ApplicationsMenu.gotoPortlet(
				category = "Users",
				panel = "Control Panel",
				portlet = "Roles");

			RoleNavigator.gotoAssignees(roleTitle = "Roles Regrole Name");

			RoleNavigator.gotoAssigneeType(assigneeType = "Segments");

			Role.addAssignees(assigneeName = "Segment usersn");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Role.tearDownCP();

			JSONUser.tearDownNonAdminUsers();

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "LPS-122800: Validate if, with the option Enable Assign roles by segments disabled, the option Assign Site Roles is disabled"
	@priority = "4"
	@uitest
	test AssignSiteRolesOptionInSegmentsEditor {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SegmentationAssigningRoles#AssignSiteRolesOptionInSegmentsEditor";

		task("Go to segments portlet and add segment in Test Site Name"){
			Navigator.openSiteURL(siteName = "Test Site Name");

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Segments");

			LexiconEntry.gotoAdd();

			Segmentation.addProperties(propertyOption = "Screen Name");

			Segmentation.fillTitle(titleField = "Segment usersn");

			Segmentation.fillProperty(propertyField = "usersn");

			Button.clickSave();
		}

		task("Open segment's options and assert the option Assign site roles is disabled"){
			Segmentation.openSegmentOptions();

			AssertElementPresent(
				key_text = "",
				locator1 = "Dropdown#ANY_DISABLED");
		}

		task("Enable Assign roles by segment"){
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Segments",
				configurationName = "Segments Service",
				configurationScope = "System Scope");

			Segmentation.enableAssingRolesBySegmentsCheckbox();
		}

		task("Go to segments portlet"){
			Navigator.openSiteURL(siteName = "Test Site Name");

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Segments");
		}

		task("Open segment's options and assert the option Assign site roles is enabled"){
			Segmentation.openSegmentOptions();

			AssertElementPresent(
				key_text = "Assign Site Roles",
				locator1 = "Dropdown#ANY_ENABLED");
		}
	}

	@description = "LPS-121943: Validate if, with the option Enable Assign roles by segments disabled, the role is not working for the user"
	@priority = "4"
	@uitest
	test DisableAssignRolesBySegment {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SegmentationAssigningRoles#DisableAssignRolesBySegment";

		task("Log out and login with the user assigned to the segment"){
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Segments",
				configurationName = "Segments Service",
				configurationScope = "System Scope");

			Segmentation.disableAssingRolesBySegmentsCheckbox();
		}

		task("Log out and login with the user assigned to the segment"){
			User.logoutPG();

			User.loginUserPG(
				password = "test",
				userEmailAddress = "userea@liferay.com");
		}

		task("Assert the role is not working"){
			AssertElementNotPresent(locator1 = "ApplicationsMenu#PORTLET");
		}
	}

	@description = "LPS-121943: Validate if, with the option Enable Assign roles by segments enabled, the role is working for the user."
	@priority = "4"
	@uitest
	test EnableAssignRolesBySegment {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SegmentationAssigningRoles#EnableAssignRolesBySegment";

		task("Enable Assign roles by segment"){
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Segments",
				configurationName = "Segments Service",
				configurationScope = "System Scope");

			Segmentation.enableAssingRolesBySegmentsCheckbox();
		}

		task("Log out and login with the user assigned to the segment"){
			User.logoutPG();

			User.loginUserPG(
				password = "test",
				userEmailAddress = "userea@liferay.com");
		}

		task("Assert the role is working"){
			ApplicationsMenuHelper.openApplicationsMenu();

			ApplicationsMenu.gotoPanel(panel = "Applications");

			AssertElementPresent(
				key_category = "Content",
				key_portlet = "Content Dashboard",
				locator1 = "ApplicationsMenu#PORTLET");
		}
	}

}
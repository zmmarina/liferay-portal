definition {

	macro _deleteAllSegments {
		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			LexiconEntry.gotoEllipsisMenuItemNoError(menuItem = "Delete");

			AssertConfirm.waitForConfirmation(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	macro addProperties {
		var key_propertyOption = "${propertyOption}";

		Click(locator1 = "Segmentation#PROPERTIES_SEARCH");

		DragAndDrop.javaScriptDragAndDropToObject(
			locator1 = "Segmentation#ADD_FIELD_TYPE",
			locator2 = "Segmentation#ADD_FIELD_POSITION",
			value1 = "${propertyOption}");
	}

	macro changeCriterionInputDate {
		Click(
			key_criterion = "date",
			locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL");

		KeyPress(
			key_criterion = "date",
			locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
			value1 = "\BACK_SPACE");

		RobotType(locator1 = "${year}");

		KeyPress(
			key_criterion = "date",
			locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
			value1 = "\RIGHT");

		RobotType(locator1 = "${month}");

		KeyPress(
			key_criterion = "date",
			locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
			value1 = "\RIGHT");

		RobotType(locator1 = "${day}");
	}

	macro changeCriterionInputEmailAddress {
		var key_criterionInputOption = "${criterionInputOption}";

		Click(locator1 = "Segmentation#CRITERION_INPUT");

		Click(
			locator1 = "Segmentation#CRITERION_INPUT_OPTION",
			value1 = "${criterionInputOption}");
	}

	macro clickAssignSiteRolesOption {
		Click(locator1 = "Segmentation#SEGMENT_OPTIONS_MENU");

		Click(locator1 = "Segmentation#ASSIGN_SITE_ROLES_OPTION");

		SelectFrame(value1 = "relative=top");

		AssertTextPresent(
			locator1 = "Segmentation#ASSIGN_SITE_ROLES_MODAL_TITLE",
			value1 = "Assign Site Roles");
	}

	macro deleteProperties {
		Click(locator1 = "Segmentation#DELETE_SEGMENT");
	}

	macro disableAssingRolesBySegmentsCheckbox {
		if (IsChecked(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX")) {
			Click.javaScriptClick(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX");
		}

		if (IsElementPresent(locator1 = "Button#SAVE")) {
			Click(locator1 = "Button#SAVE");
		}

		if (IsElementPresent(locator1 = "Button#UPDATE")) {
			Click(locator1 = "Button#UPDATE");
		}

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX");
	}

	macro editSegment {
		Click.waitForMenuToggleJSClick(
			key_tableEntry = "${entry}",
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Edit");
	}

	macro enableAssingRolesBySegmentsCheckbox {
		if (IsNotChecked.isNotCheckedNotVisible(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX")) {
			Click.javaScriptClick(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX");
		}

		if (IsElementPresent(locator1 = "Button#SAVE")) {
			Click(locator1 = "Button#SAVE");
		}

		if (IsElementPresent(locator1 = "Button#UPDATE")) {
			Click(locator1 = "Button#UPDATE");
		}

		AssertChecked.assertCheckedNotVisible(locator1 = "Segmentation#ENABLE_ASSIGN_ROLES_SEGMENTS_CHECKBOX");
	}

	macro fillProperty {
		if (isSet(criterionKey)) {
			Type(
				key_criterion = "key",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${criterionKey}");

			Type(
				key_criterion = "value",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${criterionValue}");
		}

		else if (isSet(resolutionValue)) {
			Type(
				key_criterion = "decimal-number",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${resolutionValue}");
		}

		else {
			Type(
				locator1 = "Segmentation#PROPERTY_FIELD",
				value1 = "${propertyField}");
		}
	}

	macro fillTitle {
		Click(locator1 = "Segmentation#TITLE_FIELD");

		Type(
			locator1 = "Segmentation#TITLE_FIELD",
			value1 = "${titleField}");
	}

	macro openSegmentOptions {
		Click(locator1 = "Segmentation#SEARCH_FIELD");

		Type(
			locator1 = "Segmentation#SEARCH_FIELD",
			value1 = "${segmentName}");

		KeyPress(
			locator1 = "Segmentation#SEARCH_FIELD",
			value1 = "\ENTER");

		if (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");
		}
	}

	macro openSegmentsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_segments_web_internal_portlet_SegmentsPortlet");
	}

	macro selectOrganization {
		Click(locator1 = "Segmentation#ORGANIZATION_MENU");
	}

	macro selectSession {
		Click(locator1 = "Segmentation#SESSION_MENU");
	}

	macro selectTag {
		Click(locator1 = "Segmentation#ADD_TAG_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Click(locator1 = "Segmentation#SEARCH_TAG_FIELD");

		Type(
			locator1 = "Segmentation#SEARCH_TAG_FIELD",
			value1 = "${tagOption}");

		KeyPress(
			locator1 = "Segmentation#SEARCH_TAG_FIELD",
			value1 = "\ENTER");

		Click(locator1 = "Segmentation#TAG_OPTION");

		SelectFrameTop();
	}

	macro tearDown {
		while (IsElementPresent(locator1 = "Segmentation#SEGMENT_LIST")) {
			Segmentation._deleteAllSegments();
		}

		AssertElementNotPresent(locator1 = "Segmentation#SEGMENT_LIST");
	}

	macro viewMembers {
		var key_memberName = "${memberName}";

		Click(locator1 = "Segmentation#VIEW_MEMBERS_BUTTON");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		AssertTextPresent(
			locator1 = "Segmentation#MEMBER_NAME_LISTED",
			value1 = "Test Test");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Segmentation#CLOSE_MODAL");

		SelectFrame(value1 = "relative=top");
	}

	macro viewSegment {
		if (isSet(criterionKey)) {
			AssertValue(
				key_criterion = "key",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${criterionKey}");

			AssertValue(
				key_criterion = "value",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${criterionValue}");
		}

		else if (isSet(resolutionValue)) {
			AssertValue(
				key_criterion = "decimal-number",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${resolutionValue}");
		}

		else if (isSet(date)) {
			AssertValue(
				key_criterion = "date",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "${date}");
		}

		else {
			AssertValue(
				locator1 = "Segmentation#PROPERTY_FIELD",
				value1 = "${propertyField}");
		}
	}

}
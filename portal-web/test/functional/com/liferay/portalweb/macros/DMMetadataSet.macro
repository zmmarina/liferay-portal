definition {

	macro addMultipleFields {
		DMMetadataSet.gotoFieldsTab();

		for (var fieldType : list "${fieldType}") {
			var key_fieldType = "${fieldType}";

			DoubleClick(locator1 = "DocumentsAndMediaMetadataSets#ADD_FIELD_TYPE");

			AssertElementPresent(locator1 = "DocumentsAndMediaMetadataSets#SIDEBAR_ACTIVE_BASIC_TAB");

			DMMetadataSet.editFieldLabel(fieldType = "${fieldType}");

			DMMetadataSet.gotoBack();
		}

		DDLDataDefinition.save();
	}

	macro addTitle {
		LexiconEntry.gotoAdd();

		var key_headerTitle = "New Metadata Set";

		AssertTextEquals(
			locator1 = "Dynamicdatamapping#HEADER_TITLE",
			value1 = "New Metadata Set");

		PortletEntry.inputName(name = "${metadataSetName}");
	}

	macro delete {
		if (isSet(searchMetadataSet)) {
			Search.searchCP(searchTerm = "${metadataSetName}");
		}

		if (isSet(deleteViaManagementBar)) {
			LexiconEntry.selectEntry(rowEntry = "${metadataSetName}");

			ClickNoError(locator1 = "Icon#DELETE");
		}
		else {
			LexiconEntry.openEntryMenu(rowEntry = "${metadataSetName}");

			MenuItem.clickNoError(menuItem = "Delete");
		}

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		if ("${metadataSetHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The structure cannot be deleted because it is required by one or more structure links.");
		}
		else if ("${deleteViaManagementBar}" == "true") {
			Alert.viewSuccessMessage();

			AssertElementNotPresent(locator1 = "Icon#DELETE");

			LexiconEntry.viewNoEntry(rowEntry = "${metadataSetName}");
		}
		else {
			Alert.viewSuccessMessage();

			if (isSet(searchMetadataSet)) {
				AssertElementPresent(locator1 = "Link#CLEAR");
			}

			AssertElementNotPresent(
				key_metadataSetName = "${metadataSetName}",
				locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_NAME");
		}
	}

	macro editFieldLabel {
		Pause(locator1 = "1000");

		Type(
			key_fieldName = "label",
			locator1 = "FormFields#TEXT_FIELD",
			value1 = "${fieldType}");

		if (!(isSet(fieldName))) {
			var fieldName = StringUtil.replace("${fieldType}", " ", "");
		}

		DMMetadataSet.gotoAdvancedTab();

		AssertTextEquals(
			locator1 = "FormFields#FIELD_NAME_EDIT",
			value1 = "${fieldName}");

		DMMetadataSet.gotoBasicTab();

		AssertTextEquals(
			locator1 = "DocumentsAndMediaMetadataSets#FIELD_LABEL",
			value1 = "${fieldType}");
	}

	macro gotoAdvancedTab {
		Click(locator1 = "DocumentsAndMediaMetadataSets#SIDEBAR_ADVANCED_TAB");
	}

	macro gotoBack {
		Click.pauseClickAt(locator1 = "DocumentsAndMediaMetadataSets#SIDEBAR_BACK");
	}

	macro gotoBasicTab {
		Click(locator1 = "DocumentsAndMediaMetadataSets#SIDEBAR_BASIC_TAB");
	}

	macro gotoFieldsTab {
		Click(locator1 = "DocumentsAndMediaMetadataSets#SIDEBAR_FIELDS_TAB");
	}

	macro selectMetadataSetsFrame {
		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			SelectFrameTop();

			SelectFrame(locator1 = "IFrame#MODAL_BODY");
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Documents and Media");

		DMNavigator.gotoMetadataSets();

		DMMetadataSet.selectMetadataSetsFrame();

		Search.searchCP(searchTerm = "DM");

		while (IsElementPresent(locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_TABLE_ACTIONS_1")) {
			AssertClick(
				locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_TABLE_ACTIONS_1",
				value1 = "Actions");

			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
	}

	macro validateCantInFieldset {
		var key_fieldLabel = "${key_fieldLabel}";

		Click(locator1 = "FormFields#LABEL");

		AssertElementNotPresent(locator1 = "FormViewBuilder#SIDEBAR_ADVANCED_TAB");
	}

	macro viewDefaultCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no results.");

		AssertElementNotPresent(
			key_metadataSetName = "Google Docs Metadata",
			locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_NAME");
		AssertElementNotPresent(
			key_metadataSetName = "Google Docs Metadata",
			locator1 = "DocumentsAndMediaMetadataSets#METADATA_SETS_NAME");
	}

	macro viewMetadataSetNotFoundCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No results were found.");
	}

}
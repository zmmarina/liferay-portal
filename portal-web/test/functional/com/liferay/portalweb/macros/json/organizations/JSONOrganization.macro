definition {

	macro addOrganization {
		Variables.assertDefined(parameterList = "${organizationName}");

		if (isSet(parentOrganizationName)) {
			var parentOrganizationId = JSONOrganization.getOrganizationIdByName(
				organizationName = "${parentOrganizationName}"
			);
		}

		JSONOrganizationAPI._addOrganization(
			organizationName = "${organizationName}",
			organizationSite = "${organizationSite}",
			organizationType = "${organizationType}",
			parentOrganizationId = "${parentOrganizationId}");
	}

	macro addOrganizationToDepot {
		Variables.assertDefined(parameterList = "${organizationName},${depotName}");

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		var depotId = JSONUserSetter.setGroupId(
			groupName = "${depotName}",
			site = "false");

		JSONOrganizationAPI._addOrganizationToSite(
			groupId = "${depotId}",
			organizationId = "${organizationId}");
	}

	macro addOrganizationToSite {
		Variables.assertDefined(parameterList = "${organizationName},${siteName}");

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}"
		);

		var groupId = JSONUserSetter.setGroupId(groupName = "${siteName}");

		JSONOrganizationAPI._addOrganizationToSite(
			groupId = "${groupId}",
			organizationId = "${organizationId}");
	}

	macro assignUserToOrganization {
		Variables.assertDefined(parameterList = "${organizationName},${userEmailAddress}");

		if (isSet(parentOrganizationName)) {
			var parentOrganizationId = JSONOrganization.getOrganizationIdByName(
				organizationName = "${parentOrganizationName}"
			);
		}

		var organizationId = JSONOrganization.getOrganizationIdByName(
			organizationName = "${organizationName}",
			parentOrganizationId = "${parentOrganizationId}"
		);

		var userId = JSONUserAPI._getUserIdByEmailAddress(userEmailAddress = "${userEmailAddress}");

		JSONOrganizationAPI._assignUserToOrganization(
			organizationId = "${organizationId}",
			userId = "${userId}");
	}

	macro deleteOrganizationByName {
		Variables.assertDefined(parameterList = "${organizationName}");

		if (isSet(parentOrganizationName)) {
			var parentOrganizationId = JSONOrganization.getOrganizationIdByName(
				organizationName = "${parentOrganizationName}"
			);

			var organizationId = JSONOrganization.getOrganizationIdByName(
				organizationName = "${organizationName}",
				parentOrganizationId = "${parentOrganizationId}"
			);
		}

		else {
			var organizationId = JSONOrganization.getOrganizationIdByName(
				organizationName = "${organizationName}"
			);
		}

		JSONOrganizationAPI._deleteOrganization(organizationId = "${organizationId}");
	}

	macro getOrganizationIdByName {
		Variables.assertDefined(parameterList = "${organizationName}");

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(parentOrganizationId))) {
			var parentOrganizationId = "0";
		}

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organizations/company-id/${companyId}/parent-organization-id/${parentOrganizationId} \
				-u test@liferay.com:test
		''';

		var organizationId = JSONCurlUtil.get("${curl}", "$.[?(@['name'] == '${organizationName}')]['organizationId']");

		return "${organizationId}";
	}

	macro getOrganizationName {
		Variables.assertDefined(parameterList = "${organizationId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/organization/get-organization/organization-id/${organizationId} \
				-u test@liferay.com:test
		''';

		var organizationName = JSONCurlUtil.get("${curl}", "$['name']");

		return "${organizationName}";
	}

}
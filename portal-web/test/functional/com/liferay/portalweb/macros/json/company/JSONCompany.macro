definition {

	macro getCompanyId {
		if (!(isSet(portalInstanceName))) {
			var portalInstanceName = JSONCompany.getPortalInstanceName();
		}

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(creatorEmailAddress))) {
			if ("${authenticationMethod}" == "By Screen Name") {
				var creatorEmailAddress = "test";
			}
			else {
				var creatorEmailAddress = "test@liferay.com";
			}
		}

		if (!(isSet(creatorPassword))) {
			var creatorPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/company/get-company-by-virtual-host/virtual-host/${portalInstanceName} \
				-u ${creatorEmailAddress}:${creatorPassword}
		''';

		var companyId = JSONCurlUtil.get("${curl}", "$['companyId']");

		return "${companyId}";
	}

	macro getPortalInstanceName {
		var testNameSkipPortalInstance = PropsUtil.get("test.name.skip.portal.instance");
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if (("${testPortalInstance}" == "true") && (!(IsTestName(locator1 = "", value1 = "${testNameSkipPortalInstance}")))) {
			var portalURL = JSONCompany.getPortalURL();

			var portalInstanceName = JSONCompany.parsePortalInstanceURL(portalURL = "${portalURL}");

			return "${portalInstanceName}";
		}
		else {
			return "localhost";
		}
	}

	macro getPortalURL {
		var portalURL = PropsUtil.get("portal.url");

		return "${portalURL}";
	}

	macro parsePortalInstanceURL {
		Variables.assertDefined(parameterList = "${portalURL}");

		var portalInstanceName = StringUtil.replace("${portalURL}", "http://", "");
		var portalInstanceName = StringUtil.replace("${portalInstanceName}", ":8080", "");

		return "${portalInstanceName}";
	}

}
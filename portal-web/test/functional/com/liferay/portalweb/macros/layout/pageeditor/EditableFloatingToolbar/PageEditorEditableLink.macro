definition {

	macro _selectContent {
		var itemSelectorContentInputValue = selenium.getElementValue("//label[normalize-space(text())='Content']/following-sibling::div//input[contains(@class,'item-selector__content-input')]");

		if ("${itemSelectorContentInputValue}" == "") {
			Click(
				key_fieldLabel = "Content",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Content",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		NavItem.click(navItem = "${navItem}");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		Click(
			key_card = "${entryTitle}",
			locator1 = "Card#CARD_TITLE");

		Pause(locator1 = "3000");
	}

	macro _selectField {
		Select(
			locator1 = "PageEditorEditableLink#FIELD",
			value1 = "${field}");
	}

	macro gotoEditableFieldLink {
		PageEditor.clickEditableField(
			contributed = "${contributed}",
			fragmentName = "${fragmentName}",
			id = "${id}");

		PageEditor.gotoTab(tabName = "Selection");

		AssertClick(
			key_navItem = "Link",
			locator1 = "PageEditor#NAV_ITEM_BUTTON",
			value1 = "Link");
	}

	macro mapEditableFieldToPage {
		PageEditorEditableLink.gotoEditableFieldLink(
			fragmentName = "${fragmentName}",
			id = "${id}");

		Select(
			key_fieldLabel = "Link",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "Page");

		Pause(locator1 = "3000");

		var itemSelectorContentInputValue = selenium.getElementValue("//label[normalize-space(text())='Page']/following-sibling::div//input[contains(@class,'item-selector__content-input')]");

		if ("${itemSelectorContentInputValue}" == "") {
			Click(
				key_fieldLabel = "Page",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Page",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Portlet.expandTree();

		AssertClick.assertPartialTextClickAt(
			key_nodeName = "${pageName}",
			locator1 = "Treeview#NODE_UNSELECTED",
			value1 = "${pageName}");

		SelectFrameTop();

		PageEditor.waitForAutoSave();
	}

	macro mapURLToAsset {
		PageEditorEditableLink._gotoFromContentField();

		if (isSet(entryTitle)) {
			PageEditorEditableLink._selectContent(
				entryTitle = "${entryTitle}",
				navItem = "${navItem}");
		}

		if (isSet(source)) {
			Select(
				key_fieldLabel = "Source",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${source}");
		}

		PageEditorEditableLink._selectField(field = "${field}");
	}

	macro updateURL {
		Variables.assertDefined(parameterList = "${url}");

		Type(
			locator1 = "PageEditorEditableLink#URL",
			value1 = "${url}");

		Click(
			key_sidebarButton = "Selection",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

		if ("${target}" == "Blank") {
			while (IsNotChecked.isNotCheckedNotVisible(checkboxName = "Open in a new tab.", locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click.clickAtNotVisible(
					checkboxName = "Open in a new tab.",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Pause(locator1 = "3000");
		}
		else if ("${target}" == "Self") {
			while (IsChecked.isNotCheckedNotVisible(checkboxName = "Open in a new tab.", locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click.clickAtNotVisible(
					checkboxName = "Open in a new tab.",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Pause(locator1 = "3000");
		}

		PageEditor.waitForAutoSave();
	}

}